#!/home/v-perlbrew/perl5/perlbrew/bin/perl
use lib '/usr/share/perl5';
use 5.010;
use strict;
use Munin::Plugin;
use String::Truncate qw(elide);
use Encode;

use lib '/etc/munin/our-plugins';
use MuninOSMUtil;

binmode STDOUT, ":utf8";

my $user = $ENV{PGUSER};
my $pass = $ENV{PGPASSWORD};
my $host = $ENV{PGHOST} // 'localhost';
my $port = $ENV{PGPORT} // 5432;
my $dbnm = $ENV{PGDATABASE};

# Black/whitelist
my @k_blacklist = split /\s+/, $ENV{k_blacklist} // '';
my @k_whitelist = split /\s+/, $ENV{k_whitelist} // '';

my @where;
push @where => map { qq[k != '$_'] } @k_blacklist;
push @where => map { qq[k  = '$_'] } @k_whitelist;
my $where = join ' AND ', @where; 

my $l_v_only = $ENV{label_v_only};
my $limit = $ENV{limit};
my $what = $ENV{primitive}; $what =~ s/s$//;
my $What = ucfirst $what;
my $whats = $what.'s';
my $Whats = $What.'s';
my $title = $ENV{graph_title} // "The number of $what tags in OSM by key=value";
my $desc = $ENV{graph_info}   // "The number of $what tags in the OpenStreetMap database by key=value";

my $ret = MuninOSMUtil::osm_cache(
    $ENV{MUNIN_STATEFILE},
    '/var/www/osm.nix.is/latest/Iceland.osm.bz2',
    sub {
        require DBI;
        my $dbh = DBI->connect("dbi:Pg:dbname=$dbnm;host=$host;port=$port", $user, $pass);
        my $query = <<QUERY;
SELECT count(*) as c, k, v
FROM current_${what}_tags
WHERE $where
GROUP BY k, v ORDER BY c DESC LIMIT $limit;
QUERY
        $dbh->selectall_arrayref($query);
    },
);

my @tags = @$ret;

given ($ARGV[0]) {
    when ("config") {
        print <<END;
graph_title $title
graph_args --base 1000 -l 0
graph_vlabel $what tags properties
graph_category OpenStreetMap
graph_info $desc
END
        for my $tag (@tags) {
            my ($count, $k, $v) = @$tag;
            my $fieldname = clean_fieldname("$k=$v");
            my $label = $l_v_only ? $v : "$k=$v";
            my $l = decode('utf-8', $label);
            $label = elide($l, 30, { truncate => 'middle' });
            print <<END;
$fieldname.info The number of ${what} tags with $k=$v
$fieldname.label $label
$fieldname.type GAUGE
END
        }
    }
    default {
        for my $tag (@tags) {
            my ($count, $k, $v) = @$tag;
            my $fieldname = clean_fieldname("$k=$v");
            print <<END;
$fieldname.value $count
END
}
    }
}
