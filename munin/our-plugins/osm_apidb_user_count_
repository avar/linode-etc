#!/home/v-perlbrew/perl5/perlbrew/bin/perl
use lib '/usr/share/perl5';
use 5.010;
use strict;
use Munin::Plugin;

use lib '/etc/munin/our-plugins/lib';
use Munin::Cache::Tiny;

my $user = $ENV{PGUSER};
my $pass = $ENV{PGPASSWORD};
my $host = $ENV{PGHOST} // 'localhost';
my $port = $ENV{PGPORT} // 5432;
my $dbnm = $ENV{PGDATABASE};

my $limit = $ENV{limit};
my $what = $ENV{primitive}; $what =~ s/s$//;
my $What = ucfirst $what;
my $whats = $what.'s';
my $Whats = $What.'s';
my $title = $ENV{graph_title} // "$What edits by user";
my $desc = $ENV{graph_info}   // "The number of ${what}s most recently edited by a given user";

my $ret = munin_cache_invalidated_on_file_change(
    $ENV{MUNIN_STATEFILE},
    '/var/www/osm.nix.is/latest/Iceland.osm.bz2',
    sub {
        require DBI;
        my $dbh = DBI->connect("dbi:Pg:dbname=$dbnm;host=$host;port=$port", $user, $pass);
        my $query = <<QUERY;
SELECT count(*) as c, users.display_name as dn
FROM current_$whats, changesets, users
WHERE current_$whats.changeset_id = changesets.id
      AND changesets.user_id = users.id
GROUP BY dn
ORDER BY c DESC
LIMIT $limit
QUERY
        $dbh->selectall_arrayref($query);
    },
);

my @users = @$ret;

given ($ARGV[0]) {
    when ("config") {
         print <<END;
graph_title $title
graph_args --base 1000 -l 0
graph_vlabel ${What}s by user
graph_category OpenStreetMap
graph_info $desc
END
        for my $user (@users) {
            my ($edits, $name) = @$user;
            my $fieldname = clean_fieldname($name);
            print <<END;
$fieldname.info The number of ${what} edits by $name
$fieldname.label $name
$fieldname.type GAUGE
END
        }
    }
    default {
        for my $user (@users) {
            my ($edits, $name) = @$user;
            my $fieldname = clean_fieldname($name);
            print <<END;
$fieldname.value $edits
END
        }
    }
}
