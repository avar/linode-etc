#!/home/v-perlbrew/perl5/perlbrew/bin/perl
use lib '/usr/share/perl5';
use 5.012;
use warnings;
use Munin::Plugin;

my $host = $ENV{host};

my %programs = netstat_host($host);

given ($ARGV[0]) {
    when ("config") {
        print <<END;
graph_title Netstat: Programs listening on $host
graph_args --base 1000 -l 0
graph_vlabel programs
graph_category network
graph_info Programs listening on $host by name
END
        while (my ($program, $ports) = each %programs) {
            my $fieldname = clean_fieldname($program);
            my $ports_str = join ', ', map { qq['$_'] } sort { $a <=> $b } @$ports;
            print <<END;
$fieldname.label $program
$fieldname.type GAUGE
$fieldname.draw AREASTACK
$fieldname.info The number of programs with the name "$program" listening on ports: $ports_str
END
        }
    }
    default {
        while (my ($program, $ports) = each %programs) {
            my $fieldname = clean_fieldname($program);
            my $count = @$ports;
            print <<END;
$fieldname.value $count
END
        }
    }
}

sub netstat_host
{
    my ($host) = @_;

    chomp(my @lines = qx[netstat -ldnp | egrep "\\bLISTEN\\b" | egrep "0 $host"]);


    my %programs;
    for my $line (@lines) {
        my @line = split /\s+/, $line;

        my ($host_port, $pid_name) = @line[3,6];

        my ($port) = $host_port =~ m[:(\d+)$];
        my ($pid)  = $pid_name =~ m[^(.*?)/];

        chomp(my $program = qx[ps h -o %a $pid]);

        # Sanitize program names
        $program =~ s[^/\S+/][];
        if ($program =~ m[^(\S+) -]) { $program = $1 }
        $program =~ s/\s+$//;

        push @{ $programs{$program} } => $port;
    }
    %programs;
}
