#!/usr/bin/env perl
use autodie;
use Modern::Perl;
use Apache::ConfigParser;
use Template;
use Data::Dump 'dump';

my @configs = glob "/etc/apache2/sites-enabled/*";
my $DATA = join '', <DATA>;

my @vhost = get_vhosts(@configs);

for my $vhost (@vhost) {
    my ($servername, @serveralias) = @$vhost;
    my $config = get_config($servername, \@serveralias, \@vhost);

    open my $fh, ">", "/etc/awstats/awstats.$servername.conf";
    print $fh $config;
    close $fh;
}

sub get_vhosts {
    my @configs = @_;
    my @vhost;

    for my $config (@configs) {
        my ($servername, @serveralias) = get_servername_serveralias($config);

        push @vhost => [ $servername, \@serveralias ];
    }

    return @vhost;
}

sub get_config {
    my ($servername, $serveralias, $vhost) = @_;

    my @hosts = sort {
        $a->{servername} cmp $b->{servername}
    } map {
        my $sn = $_->[0];
        {
            servername => $sn,
            active     => ($sn eq $servername)
        }
    } @$vhost;

    my $hosts_html = hosts_html(@hosts);

    my $ret;
    Template->new->process(
        \$DATA,
        {
            program     => $0,
            servername  => $servername,
            serveralias => $serveralias,
            hosts_html  => $hosts_html,
        },
        \$ret,
    );

    return $ret;
}

sub hosts_html {
    my (@hosts) = @_;
    my $html;

    $html .= "<p>";

    my @host_html;
    for my $host (@hosts) {
        my ($sn, $active) = @$host{qw( servername active ) };
        my $h;
        $h .= qq[<a href="?config=$sn&framename=mainright" title="Statistics for $sn">];
        $h .= qq[<b>] if $active;
        $h .= qq[$sn];
        $h .= qq[</b>] if $active;
        $h .= qq[</a>];
        push @host_html => $h;
    }

    $html .= join ' | ', @host_html;
    $html .= "</p>";

    return $html;
}

sub get_servername_serveralias {
    my $file = shift;

    my $c1 = Apache::ConfigParser->new;
    my $rc = $c1->parse_file($file);

    unless ($rc) {
        say STDERR $c1->errstr;
        exit 1;
    }

    my $root = $c1->root;

    my @directives = $root->daughters;

    # Get the first directive's name.
    my (@d) =$directives[0]->daughters;

    my ($servername, @serveralias);

    $servername  = $d[0]->value if $d[0]->name eq 'servername';
    @serveralias = $d[1]->value if $d[1]->name eq 'serveralias';


    return ($servername, @serveralias);
}

__DATA__
# This configuration file has been generated by [% program %]
# do not edit directly or check into Git

Include "/etc/awstats/awstats.conf"
LogFile="[% IF servername == 'awstats.nix.is' %]/var/log/apache2/access.log[% ELSE %]/var/log/apache2/[% servername %]/access.log[% END %]"
SiteDomain="[% servername %]"
# This has already been normalized by vlogger(1) to the ServerName
HostAliases="[% servername %]"
HTMLHeadSection="[% hosts_html %]"
