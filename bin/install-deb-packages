#!/usr/bin/perl
use v5.28.0;
use strict;
use warnings FATAL => 'all';
use Cwd 'getcwd';
use File::Temp 'tempdir';

sub config {
    my ($self) = @_;
    my %config;

    open my $fh, '<', '/etc/deb-packages' or die $!;

    while (my $line = <$fh>) {
        chomp $line;
        for ($line) {
            # comment
            if (/^#/ or /^$/) { next }
            # Custom action
            if (/^ (?<action> [^#\s]+ ): \s* (?<package> .+? ) \s* (?:$|\#) /x) {
                push @{$config{ $+{action} }} => $+{package};
                next;
            }
            # Install is the default action
            if (/^ (?<package>[^#\s]+ ) /x) {
                push @{$config{install}} => $+{package};
                next;
            }
            die "Can't parse line:<$line>";
        }
    }
    close $fh or die $!;

    return \%config;
}

sub run {
    my $config = config();

    for my $action (qw/ install build-dep remove /) {
        if (my @packages = @{ delete $config->{$action} || [] }) {
            system "apt $action -y @packages" and die $!;
        }
    }

    return;
}

die "$0 needs to run with root privileges\n" if $> != 0;

run();

=encoding utf8

=head1 NAME

install-deb-packages - Install Debian packages listed in /etc/deb-packages

=head1 SYNOPSIS

Run B<install-deb-packages> without arguments and it will install all
the Debian packages in F</etc/deb-packages> which have not been
installed yet.
